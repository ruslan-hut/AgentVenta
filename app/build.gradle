plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
    id 'com.google.gms.google-services'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.devtools.ksp'
    id 'com.google.dagger.hilt.android'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdk 34
    def _patch
    def versionPropsFile = file("version.properties")
    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        _patch = versionProps['PATCH'].toInteger() + 1
        versionProps['PATCH'] = _patch.toString()
        versionProps.store(versionPropsFile.newWriter(), null)
    } else {
        throw new Exception("Could not read version.properties!")
    }
    def _versionName = "3.0." + _patch
    defaultConfig {
        applicationId "ua.com.programmer.agentventa"
        minSdkVersion 19
        targetSdkVersion 34
        versionCode _patch
        versionName _versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled = true
        ksp {
            arg('room.schemaLocation', "$projectDir/schemas")
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    flavorDimensions = ["version"]
    productFlavors {
        standart {
            dimension "version"
            minSdkVersion 21
            targetSdkVersion 34
        }
        beta {
            dimension "version"
            minSdkVersion 21
            targetSdkVersion 34
            versionNameSuffix 'Î²'
        }
    }
    buildFeatures {
        dataBinding true
        viewBinding true
    }
    namespace 'ua.com.programmer.agentventa'
}

dependencies {
    implementation 'com.google.guava:guava:32.1.2-android'

    implementation 'com.google.android.material:material:1.10.0'

    //androidx
    implementation 'androidx.legacy:legacy-support-core-utils:1.0.0'
    implementation 'androidx.browser:browser:1.6.0'
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.preference:preference-ktx:1.2.1'

    // Lifecycle libraries
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.6.2"

    //firebase
    implementation platform('com.google.firebase:firebase-bom:32.3.1')
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-firestore'
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-crashlytics'
    //play service
    implementation 'com.google.android.gms:play-services-location:21.0.1'
    implementation 'com.google.android.gms:play-services-maps:18.2.0'
    //maps
    implementation 'com.google.maps.android:android-maps-utils:3.5.3'
    //volley
    implementation 'com.android.volley:volley:1.2.1'
    //glide
    implementation 'com.github.bumptech.glide:glide:4.16.0'

    //external libs
    //implementation files('libs/commons-net-3.6.jar')
    //implementation files('libs/ksoap2-no-okhttp3.jar')

    //image pinch-to-zoom support
    implementation 'io.github.igreenwood.loupe:loupe:1.2.2'

    // Hilt
    implementation 'com.google.dagger:hilt-android:2.48'
    kapt 'com.google.dagger:hilt-compiler:2.48'
    implementation 'androidx.hilt:hilt-navigation-compose:1.1.0'

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:2.7.5"
    implementation "androidx.navigation:navigation-ui-ktx:2.7.5"

    // Room
    def room_version = "2.6.0"
    implementation "androidx.room:room-common:${room_version}"
    implementation "androidx.room:room-runtime:${room_version}"
    ksp "androidx.room:room-compiler:${room_version}"
    implementation "androidx.room:room-ktx:${room_version}"

    // Retrofit (+ Moshi, Gson)
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:${retrofit_version}"
    implementation "com.squareup.retrofit2:converter-moshi:${retrofit_version}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofit_version}"
    implementation "com.squareup.okhttp3:logging-interceptor:4.11.0"

    // CameraX dependencies
    def camerax_version = "1.3.0"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-view:${camerax_version}"

}

// Allow references to generated code
kapt {
    correctErrorTypes true
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
